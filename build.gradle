/*
 * Copyright Consensys Software Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with
 * the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on
 * an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 */
import com.github.jk1.license.filter.LicenseBundleNormalizer

plugins {
  id 'java-gradle-plugin'
  id 'com.diffplug.spotless' version '7.0.3'
  id 'com.github.jk1.dependency-license-report' version '2.9'
  id 'com.gradle.plugin-publish' version '2.0.0'
}

gradlePlugin {
  website = 'https://github.com/Consensys/besu-plugin-gradle-plugin'
  vcsUrl = 'https://github.com/Consensys/besu-plugin-gradle-plugin.git'
  plugins {
    register('besuPluginLibrary') {
      id = 'net.consensys.besu-plugin-library'
      displayName = 'Plugin to help developing Besu plugins'
      description = 'It is the equivalent of the java-library plugin enhanced for Besu plugin development'
      implementationClass = 'net.consensys.gradle.BesuPluginLibrary'
      tags.set(['besu', 'ethereum'])
    }
    register('besuPluginDistribution') {
      displayName = 'Plugin to help developing and packaging Besu plugins'
      description = 'It is the equivalent of the distribution plugin, extends the net.consensys.besu-plugin-library plugin with specific packaging rules for Besu plugin distribution'
      id = 'net.consensys.besu-plugin-distribution'
      implementationClass = 'net.consensys.gradle.BesuPluginDistribution'
      tags.set(['besu', 'ethereum'])
    }
  }
}

group = 'net.consensys.gradle.besu-plugin'
version = '0.1.5-SNAPSHOT'

publishing {
  repositories {
    maven {
      name = 'localPluginRepository'
      url = file('../besu-plugin-gradle-plugin-artifacts')
    }
  }
}

licenseReport {
  allowedLicensesFile = new File("$rootDir/gradle/allowed-licenses.json")

  filters = [
    new LicenseBundleNormalizer(bundlePath: "$rootDir/gradle/license-normalizer-bundle.json")
  ]
}

configure(allprojects) {
  java {
    sourceCompatibility = 21
    targetCompatibility = 21
  }

  repositories {
    mavenCentral()
    mavenLocal()
  }

  apply plugin: 'com.diffplug.spotless'

  spotless {
    java {
      // This path needs to be relative to each project
      target 'src/**/*.java'
      removeUnusedImports()
      googleJavaFormat('1.25.2')
      importOrder 'org.hyperledger', 'java', ''
      trimTrailingWhitespace()
      endWithNewline()
      licenseHeaderFile("${rootDir}/gradle/spotless/java.current.license").named("current")
    }
    // spotless check applied to build.gradle (groovy) files
    groovyGradle {
      target '*.gradle'
      greclipse('4.31').configFile(rootProject.file('gradle/spotless/greclipse.properties'))
      endWithNewline()
      licenseHeaderFile("${rootDir}/gradle/spotless/java.current.license", "(import|plugin|rootProject)").named("current")
    }
  }
}
