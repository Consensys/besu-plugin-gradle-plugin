/*
 * Copyright Consensys Software Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with
 * the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on
 * an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 */
plugins {
  id 'java-gradle-plugin'
  id 'com.diffplug.spotless' version '7.0.3'
  id 'com.gradle.plugin-publish' version '1.3.1'
}

gradlePlugin {
  plugins {
    besuPluginLibrary {
      id = 'net.consensys.besu-plugin-library'
      implementationClass = 'net.consensys.gradle.BesuPluginLibrary'
    }
    besuPluginDistribution {
      id = 'net.consensys.besu-plugin-distribution'
      implementationClass = 'net.consensys.gradle.BesuPluginDistribution'
    }
  }
}
group = 'net.consensys.gradle.besu-plugin'
version = '0.1.3-SNAPSHOT'

publishing {
  repositories {
    maven {
      name = 'localPluginRepository'
      url = file('../besu-plugin-gradle-plugin-artifacts')
    }
  }
}

test {
  useJUnitPlatform()
}

javadoc.options.addStringOption('Xdoclint:none', '-quiet')

configure(allprojects) {
  apply plugin: 'com.diffplug.spotless'

  repositories {
    mavenCentral()
    mavenLocal()
  }

  spotless {
    java {
      // This path needs to be relative to each project
      target 'src/**/*.java'
      removeUnusedImports()
      googleJavaFormat('1.25.2')
      importOrder 'org.hyperledger', 'java', ''
      trimTrailingWhitespace()
      endWithNewline()
      licenseHeaderFile("${rootDir}/gradle/spotless/java.current.license").named("current")
    }
    // spotless check applied to build.gradle (groovy) files
    groovyGradle {
      target '*.gradle'
      greclipse('4.31').configFile(rootProject.file('gradle/spotless/greclipse.properties'))
      endWithNewline()
      licenseHeaderFile("${rootDir}/gradle/spotless/java.current.license", "plugin").named("current")
    }
  }
}
